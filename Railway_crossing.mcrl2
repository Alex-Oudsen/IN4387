sort Lights = struct LIGHTS_ON | LIGHTS_OFF;
     Bell = struct BELL_ON | BELL_OFF | BELL_CHECK;
     Barriers = struct BARRIERS_UP | BARRIERS_DOWN;
     TrainTrack = struct TRAIN_ARRIVING | NO_TRAIN;
     SingleTrack = struct NONE | EB | WB;
     SafetyCheck = struct EXPECTED | OK;
     Sensor = struct TRIPPED | IDLE;

act setLightStatus, getLightStatus, comm_lightStatus: Lights;
    setBellStatus, getBellStatus, comm_bellStatus: Bell;
    setBarrierStatus, getBarrierStatus, comm_barrierStatus: Barriers;
    setTrackStatus, getTrackStatus, comm_trackStatus: TrainTrack;
    tracksensor1e, tracksensor1w, tracksensor2e, tracksensor2w, tracksensor1m, tracksensor2m: Sensor;
    setTrack1Status, getTrack1Status, setTrack2Status, getTrack2Status, comm_track1Status, comm_track2Status: SingleTrack;
    setSafetyStatus1, getSafetyStatus1, setSafetyStatus2, getSafetyStatus2, comm_safetyStatus1, comm_safetyStatus2: SafetyCheck;

proc lights(lightStatus: Lights) = 
     (lightStatus == LIGHTS_OFF) -> getTrackStatus(TRAIN_ARRIVING).lights(LIGHTS_ON)
     + (lightStatus == LIGHTS_ON) -> getBellStatus(BELL_OFF).(getTrackStatus(TRAIN_ARRIVING).lights(LIGHTS_ON) + getTrackStatus(NO_TRAIN).lights(LIGHTS_OFF))
     + setLightStatus(lightStatus).lights();

proc bell(bellStatus: Bell) = 
     (bellStatus == BELL_OFF) -> getLightStatus(LIGHTS_ON).bell(BELL_CHECK)
     + (bellStatus == BELL_ON) -> getBarrierStatus(BARRIERS_UP).bell(BELL_CHECK)
     + (bellStatus == BELL_CHECK) -> getTrackStatus(NO_TRAIN).bell(BELL_OFF)
     + (bellStatus == BELL_CHECK) -> getTrackStatus(TRAIN_ARRIVING).bell(BELL_ON)
     + (bellStatus != BELL_CHECK) -> setBellStatus(bellStatus).bell();

proc barriers(barrierStatus: Barriers) = 
     (barrierStatus == BARRIERS_UP) -> getBellStatus(BELL_ON).(getTrackStatus(NO_TRAIN).barriers(BARRIERS_UP) + getTrackStatus(TRAIN_ARRIVING).barriers(BARRIERS_DOWN))
     + (barrierStatus == BARRIERS_DOWN) -> getTrackStatus(NO_TRAIN).barriers(BARRIERS_UP)
     + setBarrierStatus(barrierStatus).barriers();

proc track1(track1Status: SingleTrack) = 
     (track1Status == NONE) -> tracksensor1e(TRIPPED).track1(WB)
     + (track1Status == NONE) -> tracksensor1w(TRIPPED).track1(EB)
     + (track1Status == EB) -> tracksensor1e(TRIPPED).track1(NONE)
     + (track1Status == WB) -> tracksensor1w(TRIPPED).track1(NONE)
     + setTrack1Status(track1Status).track1();

proc track2(track2Status: SingleTrack) = 
     (track2Status == NONE) -> tracksensor2e(TRIPPED).track2(WB)
     + (track2Status == NONE) -> tracksensor2w(TRIPPED).track2(EB)
     + (track2Status == EB) -> tracksensor2e(TRIPPED).track2(NONE)
     + (track2Status == WB) -> tracksensor2w(TRIPPED).track2(NONE)
     + setTrack2Status(track2Status).track2();

proc safety(safetyStatus1: SafetyCheck, safetyStatus2: SafetyCheck, trackStatus: TrainTrack) = 
     (trackStatus == NO_TRAIN) -> getTrack1Status(EB).safety(safetyStatus1 = EXPECTED, trackStatus = TRAIN_ARRIVING)
     + (trackStatus == NO_TRAIN) -> getTrack1Status(WB).safety(safetyStatus1 = EXPECTED, trackStatus = TRAIN_ARRIVING)
     + (trackStatus == NO_TRAIN) -> getTrack2Status(EB).safety(safetyStatus2 = EXPECTED, trackStatus = TRAIN_ARRIVING)
     + (trackStatus == NO_TRAIN) -> getTrack2Status(WB).safety(safetyStatus2 = EXPECTED, trackStatus = TRAIN_ARRIVING)
     + (safetyStatus1 == EXPECTED) -> tracksensor1m(TRIPPED).safety(safetyStatus1 = OK)
     + (safetyStatus1 == EXPECTED && safetyStatus2 == OK) -> getTrack2Status(EB).safety(safetyStatus2 = EXPECTED)
     + (safetyStatus1 == EXPECTED && safetyStatus2 == OK) -> getTrack2Status(WB).safety(safetyStatus2 = EXPECTED)
     + (safetyStatus2 == EXPECTED) -> tracksensor2m(TRIPPED).safety(safetyStatus2 = OK)
     + (safetyStatus2 == EXPECTED && safetyStatus1 == OK) -> getTrack1Status(EB).safety(safetyStatus1 = EXPECTED)
     + (safetyStatus2 == EXPECTED && safetyStatus1 == OK) -> getTrack1Status(WB).safety(safetyStatus1 = EXPECTED)
     + (trackStatus == TRAIN_ARRIVING && safetyStatus1 == OK && safetyStatus2 == OK) -> getTrack1Status(NONE)
       .(getTrack2Status(NONE).safety(trackStatus = NO_TRAIN) + getTrack1Status(EB).safety(safetyStatus1 = EXPECTED) +
       getTrack1Status(WB).safety(safetyStatus1 = EXPECTED) + getTrack2Status(EB).safety(safetyStatus2 = EXPECTED) + 
       getTrack2Status(WB).safety(safetyStatus2 = EXPECTED))
     + (trackStatus == TRAIN_ARRIVING && safetyStatus1 == OK && safetyStatus2 == OK) -> getTrack1Status(EB).safety(safetyStatus1 = EXPECTED)
     + (trackStatus == TRAIN_ARRIVING && safetyStatus1 == OK && safetyStatus2 == OK) -> getTrack1Status(WB).safety(safetyStatus1 = EXPECTED)
     + (trackStatus == TRAIN_ARRIVING && safetyStatus1 == OK && safetyStatus2 == OK) -> getTrack2Status(EB).safety(safetyStatus2 = EXPECTED)
     + (trackStatus == TRAIN_ARRIVING && safetyStatus1 == OK && safetyStatus2 == OK) -> getTrack2Status(WB).safety(safetyStatus2 = EXPECTED)
     + setSafetyStatus1(safetyStatus1).safety()
     + setSafetyStatus2(safetyStatus2).safety()
     + setTrackStatus(trackStatus).safety();

init 
     hide({comm_lightStatus, comm_bellStatus, comm_barrierStatus, comm_trackStatus,
           comm_track1Status, comm_track2Status, comm_safetyStatus1, comm_safetyStatus2},
          allow({comm_lightStatus, comm_bellStatus, comm_barrierStatus, comm_trackStatus,
                 tracksensor1e, tracksensor1w, tracksensor2e, tracksensor2w,
                 tracksensor1m, tracksensor2m, comm_track1Status, comm_track2Status,
                 comm_safetyStatus1, comm_safetyStatus2},
               comm({setLightStatus|getLightStatus -> comm_lightStatus,
                     setBellStatus|getBellStatus -> comm_bellStatus,
                     setBarrierStatus|getBarrierStatus -> comm_barrierStatus,
                     setTrackStatus|getTrackStatus -> comm_trackStatus,
                     setTrack1Status|getTrack1Status -> comm_track1Status,
                     setTrack2Status|getTrack2Status -> comm_track2Status,
                     setSafetyStatus1|getSafetyStatus1 -> comm_safetyStatus1,
                     setSafetyStatus2|getSafetyStatus2 -> comm_safetyStatus2},
                        lights(LIGHTS_OFF)
                        ||bell(BELL_OFF)
                        ||barriers(BARRIERS_UP)
                        ||track1(NONE)
                        ||track2(NONE)
                        ||safety(OK, OK, NO_TRAIN)
               )
          )
     );